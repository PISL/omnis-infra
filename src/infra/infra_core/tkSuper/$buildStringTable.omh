;  this method builds the "allstrings: string table from the database specified in the .libini
;  if there is no DB connection to stb in the .libini, nothing will happen
;  the connection to the stb DB is done with a local variable which automatically disconnects on destruct

Do toConfig.$GetDatabases() Returns llDBList
Do llDBList.$search($ref.Name='StringTable',1,0,0,0) Returns lnLine
If lnLine
	If llDBList.DAM='WebService'
		;  these columns are populated later in TRACKER.wsecLogin but we need them here first
		Do tConstants.$cols.$findname('REST_URI') Returns liColRef
		If liColRef
			;  column already exists
		Else
			;  add REST URI for the Web Server
			Do tConstants.$cols.$add('REST_URI',kCharacter,kSimplechar,255)
			Do tConstants.$cols.$add('ExternalIP',kCharacter,kSimplechar,45)     ;; allow for IPv6
			Do tConstants.$cols.$add('RESTsessionID',kCharacter,kSimplechar,100)
		End If
		If left(llDBList.Host,4)='http'     ;; added this check to allow local testing which probably won't be using SSL
			Calculate tConstants.REST_URI as con(llDBList.Host,'/api/',low(tcCurrentLibrary),'/stb/')
		Else
			Calculate tConstants.REST_URI as con('https://',llDBList.Host,llDBList.port,'/api/',tcCurrentLibrary,'/stb/')
		End If
		
		Do method getStringsFromServer Returns llStrings
		
	Else
		Do method $ConnectToDb (llDBList.Host,'stringsonly','61veM3theT@ble5',,llDBList.DAM,llDBList.DB,llDBList.port,loSession,loStatement,0) Returns lbOK     ;; ### password ###
		Do $DefineList(llStrings,'infra.tomStrings')
		Do llStrings.$SetSession(loSession,loStatement)
		Do llStrings.$fetchForLibrary()
		Do llStrings.$setSession(#NULL,#NULL,kTrue)
	End If
	
	;  we only want the actual string table entries from omStrings
	;  starting with STRINGID which is column 9
	For lnCount from 9 to llStrings.$colcount step 1
		Do llData.$cols.$add(llStrings.$cols.[lnCount].$name,kCharacter,kSimplechar)
	End For
	
	Do llData.$merge(llStrings,kTrue,kFalse,kTrue)
	
	
	Do llTables.$cols.$add('name',kCharacter,kSimplechar)
	Do llTables.$cols.$add('path',kCharacter,kSimplechar)
	Do StringTable.$gettablelist(llTables)
	Do llTables.$search($ref.name='allstrings',1,0,0,0) Returns lnLine
	If lnLine
		Do StringTable.$loadlistfromtable('allstrings') Returns llAllStrings
		Do StringTable.$unloadstringtable('allstrings')
		Do llData.$merge(llAllStrings,kTrue,kFalse,kFalse)
	End If
	
	Do StringTable.$loadtablefromlist('allstrings',,llData) Returns lnStringTableResult     ;; this will automatically remove all duplicates based on STRINGID
	
	;  set the language     ;; copied from OpenStringTable method - trConfig may not be populated yet !
	If $ctask.trConfig.Language<>''
		Calculate lcDefaultColumn as $ctask.trConfig.Language
	Else
		Calculate lcDefaultColumn as 'en'
	End If
	Do StringTable.$setcolumn(lcDefaultColumn) Returns lnResult
	If lnResult<>1
		Send to trace log {Error setting default column in string table "allstrings"}
	End If
	;  Do StringTable.$loadlistfromtable('allstrings') Returns llAllStrings
	
End If