Do method $Save

Do ilData.$clear()
If ibFetchInherited
	;  only available from RFO
	If ilClasses.RFO_CLASS<>''
		Calculate llThisTable as ilData
		Do llThisTable.$FetchClass(ilClasses.RFO_CLASS,,kTrue,,ilOrganisations.GO_SEQ,#NULL)
		
		Do ilData.$fetchInherited(ilClasses.RFO_CLASS,,ilOrganisations.GO_SEQ)
		;  highlighting the differences is not so easy
		;  inactive in RFO will not appear in ilData but will in llThisTable
		While $Loop(llThisTable)
			;  This search is flawed.  The result in ilData has no clue of whether the row came from RFO, RFL or RFG.
			;  Based on CLASS and VALUE abyway.  Added ORDER to slightly improve the accuracy but it is still flowed if the same value exists in both tables.
			Do ilData.$search($ref.RFO_CLASS=llThisTable.RFO_CLASS&$ref.RFO_VALUE=llThisTable.RFO_VALUE&$ref.RFO_ORDER=llThisTable.RFO_ORDER,1,0,0,0) Returns lbFound
			If lbFound
				Do ilData.$assignrow(llThisTable,kTrue)     ;; this will show which rows came from RFO that are active
			End If
		End While
	End If
Else
	If icColPrefix='RFO_'
		Do ilData.$FetchClass(ilClasses.RFO_CLASS,,kTrue,kFalse,ilOrganisations.GO_SEQ,#NULL)
	Else
		Do ilData.$FetchClass(ilClasses.[icColPrefix]CLASS,,kTrue,kFalse,#NULL)
	End If
End If

Do ilData.$sort($ref.[icColPrefix]ORDER,0,$ref.[icColPrefix]VALUE,0)
Calculate ilData.$smartlist as kTrue

Calculate ilOriginal as ilData

;  lets see if entries of the same CLASS exist in other REFERENCE tables
Set reference lrefPane to $cinst.$objs.TabPaneHeader.$objs.PagedPane
If icColPrefix='RFG_'
	Do toValues.$initLocal(ilClasses.RFG_CLASS,#NULL,kFalse,kTrue,#NULL) Returns llLocal     ;; exclude fixed, return all date sensitive records
	Do toValues.$initOrg(ilClasses.RFG_CLASS,#NULL,kFalse,kTrue,#NULL) Returns llOrg     ;; exclude fixed, return all date sensitive records
	
	Calculate lrefPane.$objs.RFGbaLOCAL.$enabled as (llLocal.$linecount>0)
	Calculate lrefPane.$bobjs.1089.$visible as (llLocal.$linecount>0)
	Calculate lrefPane.$objs.RFGbaORG.$enabled as (llOrg.$linecount>0)
	Calculate lrefPane.$bobjs.1090.$visible as (llOrg.$linecount>0)
	
Else If icColPrefix='RFL_'
	Do toValues.$initGlobal(ilClasses.RFL_CLASS,#NULL,kTrue,kTrue) Returns llGlobal     ;; include non-active, exclude fixed
	Do toValues.$initOrg(ilClasses.RFL_CLASS,#NULL,kTrue,kTrue) Returns llOrg     ;; include non-active, exclude fixed
	
	Calculate lrefPane.$objs.RFLbaGLOBAL.$enabled as (llGlobal.$linecount>0)
	Calculate lrefPane.$bobjs.1094.$visible as (llGlobal.$linecount>0)
	Calculate lrefPane.$objs.RFLbaORG.$enabled as (llOrg.$linecount>0)
	Calculate lrefPane.$bobjs.1095.$visible as (llOrg.$linecount>0)
	
Else     ;; RFO_
	Do toValues.$initLocal(ilClasses.RFO_CLASS,#NULL,kFalse,kTrue) Returns llLocal     ;; exclude fixed
	Do toValues.$initGlobal(ilClasses.RFO_CLASS,#NULL,kFalse,kTrue) Returns llGlobal     ;; exclude fixed
	
	Calculate lrefPane.$objs.RFObaLOCAL.$enabled as (llLocal.$linecount>0)
	Calculate lrefPane.$bobjs.1099.$visible as (llLocal.$linecount>0)
	Calculate lrefPane.$objs.RFObaGLOBAL.$enabled as (llGlobal.$linecount>0)
	Calculate lrefPane.$bobjs.1100.$visible as (llGlobal.$linecount>0)
	
End If

Calculate inRowCount as ilData.$linecount

Do $cinst.$behaviour()
Do $cinst.$redraw()