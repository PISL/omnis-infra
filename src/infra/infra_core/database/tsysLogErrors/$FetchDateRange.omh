If pFrom>dat('1 JAN 2010')|pTo>dat('1 JAN 2010')     ;; pFrom <= pTo should be checked in the calling mathod
	;  sufficient criteria
Else If pCode<>''
	;  sufficient criteria     ;; (not really)
Else If pcSearchText<>''
	;  sufficient criteria     ;; (not really)
Else
	;  not enough criteria for a modest fetch
	Calculate icValidationMessage as 'not enough search criteria'
	Quit method kFalse
End If

If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pFrom,pTo,pCode,pcSearchText,pVhostNo),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

If isnumber(pcSearchText)
	Calculate lbIncludePort as kTrue
Else
	Calculate lcTemp as replaceall(pcSearchText,'.','')     ;; may be an IP address being provided
	If isnumber(lcTemp)
		Calculate lbIncludeIP as kTrue
	End If
End If
If pos(':',pcSearchText)
	Calculate lnIncludeTimestamp as kTrue
End If

If pcSearchText<>''&pos('%',pcSearchText)=0
	Calculate pcSearchText as con('%',pcSearchText,'%')
End If
Calculate lcPredicate as ' where '

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

If not(isclear(pTo))
	Calculate pTo as dadd(kDay,1,pTo)     ;; date col to datetime
End If

Begin statement (Carriage return,Linefeed)
Sta: {select [$cinst.$selectnames()]}
Sta: {from   [$cinst.$servertablenames()]}
If pFrom>dat('1 JAN 2010')
	Sta: {[lcPredicate] SLE_CWHEN >= @[pFrom]}
	Calculate lcPredicate as ' and '
End If
If pTo>dat('1 JAN 2010')
	Sta: {[lcPredicate]  SLE_CWHEN < @[pTo]}     ;; pTo has had 1 day added to it
	Calculate lcPredicate as ' and '
End If
If pCode<>''
	Sta: {[lcPredicate]     SLE_CODE = @[pCode]}
	Calculate lcPredicate as ' and '
End If
If pcSearchText<>''
	Sta: {[lcPredicate]   (}
	Sta: {SLE_MESSAGE ilike @[pcSearchText] or SLE_CODE ilike @[pcSearchText] or SLE_SUBCODE ilike @[pcSearchText]}
	If lbIncludeIP
		Sta: {or SLE_SERVER_IP ilike @[pcSearchText]}
	End If
	If lbIncludePort
		Sta: {or  cast ( SLE_SERVER_PORT as varchar(15) ) ilike @[pcSearchText]}
	End If
	If lnIncludeTimestamp
		;  typically detects hour within a day
		Sta: {or  cast ( SLE_CWHEN as varchar(40) ) ilike @[pcSearchText]}
	End If
	Sta: {)}
	Calculate lcPredicate as ' and '
End If
If pVhostNo>0
	Sta: {[lcPredicate]     SLE_VHOST_REF = @[pVhostNo]}
	Calculate lcPredicate as ' and '
End If
End statement
Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK