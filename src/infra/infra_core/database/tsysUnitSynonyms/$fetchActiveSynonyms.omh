If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pnUOMref),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

;  this method fetches all the synonyms along with the UOM record
Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate lcWhere as 'where UOS_ACTIVE = 1 '
If len(icForeignCols)>0
	Calculate lcWhere as con(lcWhere,' and UOM_ACTIVE = 1')
End If

If pnUOMref>0
	Do $cinst.$Sql(#NULL,'',kFalse,kTrue) Returns lcSQL
	Calculate lcSQL as con(lcSQL,' and UOS_UOM_REF = @[pnUOMref]')
	Begin statement (Carriage return)
	Sta: {[lcSQL]}
	End statement
Else
	Do $cinst.$Sql(#NULL,lcWhere)
End If

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do $cinst.$sort(low($ref.UOS_CODE),0)

Quit method lbOK