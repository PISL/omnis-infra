If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pVhostNo),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

If isnumber(pcSearchText)
	Calculate lbIncludeStatus as kTrue
End If

If pcSearchText<>''&pos('%',pcSearchText)=0
	Calculate pcSearchText as con('%',pcSearchText,'%')
End If
Calculate lcPredicate as ' where '

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from   [$cinst.$servertablenames()]}
If pcSearchText<>''
	Sta: {[lcPredicate]   (}
	Sta: {AE_MESSAGE ilike @[pcSearchText] or AE_SUBJECT ilike @[pcSearchText] or AE_FAIL_DIALOGUE ilike @[pcSearchText]}
	If lbIncludeStatus
		Sta: {or  cast ( AE_FAIL_STATUS as varchar(15) ) ilike @[pcSearchText]}
	End If
	Sta: {)}
	Calculate lcPredicate as ' and '
End If
If pVhostNo>0
	Sta: {[lcPredicate]     AE_GO_REF = @[pVhostNo]}
	Calculate lcPredicate as ' and '
End If
End statement
Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK