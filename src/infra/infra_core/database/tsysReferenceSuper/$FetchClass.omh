If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pClass,pValue,pIncludeNonActive,pAppend),$cinst.$type,0,$cinst.ilRESTextensions))
	Calculate lrWebResponse as ioWeb.irResponse
	If lrWebResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: {select [$cinst.$selectnames()]}
Sta: {from  [$cinst.$servertablenames()]}
Sta: {where [icPrefix]CLASS = @[pClass]}
If pValue<>''
	Sta: {and [icPrefix]VALUE = @[pValue]}
End If
If pIncludeNonActive
	;  exclude the active items clause
Else
	Sta: {and [icPrefix]ACTIVE = 1}
End If
If pDate>dat('1 JAN 2010')
	Sta: {and (}
	;  no time frame on reord
	Sta: {([icPrefix]EFFECTIVE is null and  [icPrefix]EXPIRES is null )}
	;  valid effective date with no expiry date     ;; this is current
	Sta: {or  ([icPrefix]EFFECTIVE >= @[pDate] and  [icPrefix]EXPIRES is null )}
	;  valid effective and expiry dates
	Sta: {or  ([icPrefix]EFFECTIVE >= @[pDate] and  [icPrefix]EXPIRES <= @[pDate] )}
	;  logically peculiar but technically possible
	Sta: {or  ([icPrefix]EFFECTIVE is null and  [icPrefix]EXPIRES <= @[pDate] )}
	Sta: {)}
End If
End statement
Do $cinst.$executesqlandfetch($cinst,pAppend)

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do $cinst.$sort($ref.[icPrefix]ORDER,0,$ref.[icPrefix]VALUE,0)