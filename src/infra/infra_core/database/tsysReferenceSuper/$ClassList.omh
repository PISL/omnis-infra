If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch(lList,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(),lList.$type,1,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method lList
	Else
		Quit method kFalse
	End If
End If

Do lList.$cols.$add(con(icPrefix,'CLASS'),kCharacter,kSimplechar,15)

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: {select distinct [icPrefix]CLASS from  [$cinst.$servertablenames]}
End statement
Do $cinst.$executesqlandfetch(lList)

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do lList.$sort($ref.[icPrefix]CLASS,0)

;  suppress hidden classes
Do lList.$search(left($ref.[icPrefix]CLASS,1)='.',1,0,1,0)
Do lList.$remove(kListDeleteSelected)

Quit method lList