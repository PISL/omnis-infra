Do $cinst.$cols.$findname('COUNT') Returns liColRef
If liColRef
	;  count col already exists
Else
	Do $cinst.$cols.$add('COUNT',kInteger,k32bitint)
End If

If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pMainLang,pOfficialLang,pLanguageInClause),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

If pLanguageInClause<>''
	;  which column will this apply to ?
	Calculate pLanguageInClause as trim(pLanguageInClause)
	If pos(',',pLanguageInClause)>0
		Calculate lcFirstToken as mid(pLanguageInClause,1,pos(',',pLanguageInClause)-1)
	Else
		Calculate lcFirstToken as pLanguageInClause
	End If
	If mid(lcFirstToken,1,1)="'"
		If len(lcFirstToken)=4     ;; tConstants.DefaultLanguageCode
			Calculate lcInClauseColumn as 'LG_ISO2'
		Else If len(lcFirstToken)=5     ;; 'eng'
			Calculate lcInClauseColumn as 'LG_ISO3'
		Else
			;  error !     ;; bad parameter
			Quit method
		End If
	Else If isnumber(lcFirstToken)
		Calculate lcInClauseColumn as 'LG_SEQ'
	Else
		;  error !     ;; bad parameter
		Quit method
	End If
End If

Calculate lcPredicate as 'where '

Begin statement
Sta: {select [$cinst.$selectnames()], count(*)}
Sta: {from [$cinst.$servertablenames]}
Sta: {join  [lrLangCountry.$servertablenames()] on LC_LG_REF = LG_SEQ}

If pOfficialLang
	;  official language(s) only
	Sta: {[lcPredicate]     LC_OFFICIAL = 1}
	Calculate lcPredicate as ' and '
End If
If pMainLang
	;  main language(s) only
	Sta: {[lcPredicate]     LC_MAINLANG = 1}
	Calculate lcPredicate as ' and '
End If
If lcInClauseColumn<>''
	If lcInClauseColumn='LG_ISO3'
		Sta: {[lcPredicate]     ( LG_ISO3 in ( [pLanguageInClause] )  or  LG_ISO3_ALT in ( [pLanguageInClause] ) )}
	Else
		Sta: {[lcPredicate]     [lcInClauseColumn] in ( [pLanguageInClause] )}
	End If
	Calculate lcPredicate as ' and '
End If
Sta: {group by [$cinst.$selectnames()]}
End statement
Do $cinst.$executesqlandfetch()

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If