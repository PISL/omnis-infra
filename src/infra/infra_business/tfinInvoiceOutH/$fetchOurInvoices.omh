If $ctask.trGroupOrg.GO_SEQ>0
	;  proceed
Else
	Quit method kFalse
End If

If tlDatabases.DAM='WebService'
	;  pass parameters to web service
	;  $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) )     ;; result will be returned in pFieldReference, row() contains the parameters for the call
	Do ioWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pnEO_REF,pdFrom,pdTo,pbExactDate),$cinst.$type,0,ilRESTextensions))
	If ioWeb.irResponse.httpStatusCode=200
		;  the result should be in this object     ;; however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate inID as $ctask.trGroupOrg.GO_SEQ
Calculate lcWhereClause as con(lcWhereClause,"where FOH_GO_REF = @[inID]")
If pnEO_REF>0
	Calculate inEO_REF as pnEO_REF
	Calculate lcWhereClause as con(lcWhereClause," and FOH_EO_REF = @[$cinst.inEO_REF]")
End If
If not(isclear(pdFrom))
	Calculate idFrom as pdFrom
	If pbExactDate
		Calculate lcWhereClause as con(lcWhereClause," and FOH_DATE = @[$cinst.idFrom]")
	Else
		Calculate lcWhereClause as con(lcWhereClause," and FOH_DATE >= @[$cinst.idFrom]")
	End If
End If
If not(isclear(pdTo))
	Calculate idTo as pdTo
	Calculate lcWhereClause as con(lcWhereClause," and FOH_DATE <= @[$cinst.idTo]")
End If

Do $cinst.$Sql(,lcWhereClause,0,0)
Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK