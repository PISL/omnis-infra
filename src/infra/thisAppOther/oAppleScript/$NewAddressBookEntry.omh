;  prPerson is a row of data from db in STANDARD column format

Do lrReturn.$cols.$add('AppleScriptResult',kCharacter,kSimplechar,255)
Do lrReturn.$cols.$add('AppleScriptError',kCharacter,kSimplechar,255)
Calculate lrSaveApplescript as lrReturn

;  tell application "Address Book"
;  set thePerson to make new person with properties
;  {first name:"Ben", last name:"Waldie", organization:"Automated Workflows,
;  LLC", job title:"President"}
;  end tell
;  --> person id "778C4D8D-0A22-4918-A5B6-73361FFF8779:ABPerson" of application "Address Book"

;  AddressBook scripting was derived from     ;; http://www.mactech.com/articles/mactech/Vol.21/21.10/ScriptingAddressBook/index.html

;  personâ€‚n, pl people [inh. entry] : A person in the address book database.
;  elements
;  contains urls, addresses, phones, groups, custom dates, instant messages, social profiles, related names, emails; contained by application, groups.
;  properties
;  job title (text or missing value) : The job title of this person.
;  last name (text or missing value) : The Last name of this person.
;  first name (text or missing value) : The First name of this person.
;  title (text or missing value) : The title of this person.
;  company (boolean) : Is the current record a company or a person.
;  organization (text or missing value) : Organization that employs this person.
;  department (text or missing value) : Department that this person works for.
;  home page (text or missing value) : The home page of this person.
;  note (text or missing value) : Notes for this person.

;  nickname (text or missing value) : The Nickname of this person.
;  maiden name (text or missing value) : The Maiden name of this person.
;  suffix (text or missing value) : The Suffix of this person.
;  vcard (text or missing value, r/o) : Person information in vCard format, this always returns a card in version 3.0 format.
;  birth date (date or missing value) : The birth date of this person.
;  phonetic last name (text or missing value) : The phonetic version of the Last name of this person.
;  phonetic middle name (text or missing value) : The Phonetic version of the Middle name of this person.
;  image (TIFF picture or missing value) : Image for person.
;  name (text, r/o) : First/Last name of the person, uses the name display order preference setting in Address Book.
;  middle name (text or missing value) : The Middle name of this person.
;  phonetic first name (text or missing value) : The phonetic version of the First name of this person.

Begin text block
Text: {tell application "Address Book"} (Platform newline)
Text: {set thePerson to make new person with properties}
Text: { {first name:"[cap(prPerson.FirstName)]", last name:"[cap(prPerson.LastName)]"}
If prPerson.Organization<>''
	Text: {, organization:"[cap(prPerson.Organization)]"}
End If
If prPerson.JobTitle<>''
	Text: {, job title:"[cap(prPerson.JobTitle)]"}
End If
If prPerson.Department<>''
	Text: {, department:"[cap(prPerson.Department)]"}
End If
If prPerson.Title<>''
	Text: {, title:"[cap(prPerson.Title)]"}
End If
If prPerson.Suffix<>''
	Text: {, suffix:"[cap(prPerson.Suffix)]"}
End If
If prPerson.Note<>''
	Text: {, note:"[replaceall(prPerson.Note,kDq,'')]"}
End If
If prPerson.Website<>''
	Text: {, home page:"[cap(prPerson.Website)]"}
End If
Text: {}} (Platform newline)
Text: {end tell} (Platform newline)
End text block
Get text block lcApplescript

;  Breakpoint
Do $root.$runapplescript(lcApplescript,lrReturn.AppleScriptResult,lrReturn.AppleScriptError)

;  Adding an address to a person is slightly different than the process of adding an email address, a URL, or a phone number.
;  While you can still specify a label property for an address, addresses also possess many other properties.
;  For example, in an address entry, you may specify a street, city, state, zip code, and more. The following example
;  code demonstrates how to add a work address to a specified person in Address Book.
;  make new address at end of addresses of thePerson with properties
;  label:"Work", street:"123 Anystreet", city:"Anytown", state:"PA", zip:"99999"

If pos('person id',lrReturn.AppleScriptResult)>0
	;  person id "36572BAC-2886-48CD-B14C-142B57D43E16" of calendar id "046C23BE-2BD9-455D-A288-18E8D1AE5309" of application "iCal"
	Calculate lcTemp as lrReturn.AppleScriptResult
	Calculate lcToken as strtok(nam(lcTemp),'"')
	Calculate lcPersonID as strtok(nam(lcTemp),'"')
	
	Do method $NewElements (lcPersonID,prPerson,lrApplescriptOtherItems)
	
	
	Begin text block
	Text: {tell application "Address Book"} (Platform newline)
	Text: {save} (Platform newline)
	Text: {end tell} (Platform newline)
	End text block
	Get text block lcApplescript
	Do $root.$runapplescript(lcApplescript,lrSaveApplescript.AppleScriptResult,lrSaveApplescript.AppleScriptError)
End If

Calculate prAppleScript as lrReturn
Quit method lrReturn